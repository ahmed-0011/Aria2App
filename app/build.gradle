import com.github.triplet.gradle.androidpublisher.ReleaseStatus

plugins {
    id 'com.github.triplet.play' version '3.9.1'
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def isCi = System.getenv("CI") == "true"
android {
    namespace 'com.gianlu.aria2app'

    defaultConfig {
        compileSdk 36
        
        applicationId "com.gianlu.aria2app"
        minSdkVersion 21
        targetSdkVersion 36

        versionCode 230
        versionName "5.9.18"
    }

    if (isCi) {
        signingConfigs {
            release {
                storeFile file("../.github/keystore.jks")
                storePassword System.getenv("KEYSTORE_STORE_PASS")
                keyAlias = System.getenv("KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("KEYSTORE_KEY_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources true
            if (isCi) signingConfig signingConfigs.release
        }
    }

    flavorDimensions = ['main']
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled false
                    shrinkResources true
                }
            }
        }

        foss {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled false
                    shrinkResources false
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        warning 'MissingTranslation', 'Instantiatable'
    }

    playConfigs {
        foss {
            commit.set(false)
            enabled.set(false)
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    buildFeatures {
        buildConfig true
    }
}

play {
    defaultToAppBundles.set(true)
    enabled.set(isCi)
    serviceAccountCredentials.set(file("../.github/secrets.json"))
    track.set("production")
    releaseStatus.set(ReleaseStatus.DRAFT)
}

dependencies {
    implementation project(':CommonUtils')
    implementation project(':aria2lib')

    implementation 'com.github.tonyofrancis.Fetch:xfetch2:3.1.6'
    implementation 'com.github.tonyofrancis.Fetch:xfetch2okhttp:3.1.6'

    implementation 'commons-net:commons-net:3.10.0'
    implementation 'com.hierynomus:smbj:0.13.0'
    implementation 'com.jcraft:jsch:0.1.55'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'org.apmem.tools:layouts:1.10@aar'
    implementation 'com.github.devgianlu:Android--Library-Country-Flags:6fc6059f91'

    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'

    implementation 'com.google.android.material:material:1.12.0'
}
